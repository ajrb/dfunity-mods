Quest: ARCH001
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                    Hello, can you be discreet?
<ce>                    I really shouldn't give this job
<ce>                    to another member, but I have
<ce>                    something that I need done that
<ce>                    I don't really want anyone else
<ce>                    in the guild to find out about.
<ce>                    If I were to do this myself, my
<ce>                   absence would certainly be noticed.
<ce>
<ce>                    Do this task for me and keep it
<ce>                    quiet, then I'll help talk up your
<ce>                    reputation with the guild and give
<ce>                   you _reward_ gold. What do you say %pcf?

RefuseQuest:  [1001]
<ce>                      Have trouble keeping secrets do
<ce>                        you? Thanks for nothing.
                                     <--->
<ce>                     Fair enough, it is a delicate matter.

AcceptQuest:  [1002]
<ce>                       My name is _questgiver_.
<ce>                  There is an ancient magical text supposedly
<ce>                  hidden in ___dungeon_ that I want to acquire
<ce>                  before the Mages guild get their hands on it.
<ce>              Bring it back to me within =traveltime_ days
<ce>                  and I'll be in your debt. Here is a locator
<ce>                charge to assist you in your search.

QuestComplete:  [1004]
<ce>                Excellent. I remembered to warn you not
<ce>                to read this, didn't I?  Could be quite
<ce>                dangerous if you're not an experienced
<ce>                mage. Thanks so much for your help, and
<ce>                discretion, %pcn. As promised I will speak
<ce>                 on your behalf with the other high ranking
<ce>                   members of the Archeaologists.

QuestorPostsuccess:  [1008]
Certainly I remember you, %pcf. You brought me that text, right?

QuestorPostfailure:  [1009]
<ce>               I can't imagine a simpler assignment than
<ce>               the one I gave you -- just find a rare old
<ce>               scroll. The Mages guild have surely obtained
<ce>               if for themselves by now. I'm disapointed at
<ce>                this outcome, but I still expect and reply
<ce>                     on your discretion %pcn!

QuestLogEntry:  [1010]
%qdt:
 I met _questgiver_ of 
 the Archaeologists Guild of ___questgiver_
 who sent me on a quest to find an
 ancient magical text hidden in ___dungeon_.
 I have =traveltime_ days to get it to %g2.

Message:  1020
Zephrim alga n' nagog. Carti
 alga n' belyl. Azuth Zephrim ne'
 nagog. Partik aloy partik frae i
 eyominathraq n' toyl. Berik azuth
 n' nagog. Charlaq gimle n' maluk
 Zephrim qork.

Message:  1040
<ce>                      This must be the magical text
<ce>                     _questgiver_ sent you to find.

Message:  1041
<ce>                  Thankfully due to your high skill with Orcish language,
<ce>                   you realise you shouldn't recite this in full, unless you
<ce>                   want to be attacked by Imps of course, which you don't!

QuestTimeLapse:  [1045]
<ce>                      As you read the final word,
<ce>                      you hear two loud pops and
<ce>                        almost drop the paper.


QBN:
Item _text_ letter used 1020
Item _reward_ gold range 500 to 800

Person _questgiver_ group Questor female

Place _dungeon_ remote dungeon

Clock _traveltime_ 00:00 0 flag 17 range 0 2

Foe _imps_ is 2 Imp

--	Quest start-up:
	place item _text_ at _dungeon_ 
	start timer _traveltime_ 
	reveal _dungeon_ 
	log 1010 step 0 

_traveltime_ task:
	end quest 

_S.01_ task:
	toting _text_ and _questgiver_ clicked 
	give pc _reward_ 
	end quest 

_S.02_ task:
	clicked item _text_ 
	_text_ used saying 1020 do _read_ 
	say 1040

variable _read_
_S.03_ task:
    when not _orcish_ and _read_
    say 1045
	create foe _imps_ every 1 minutes 1 times with 100% success

_safeRead_ task:
    when _orcish_ and _read_
    say 1041

_orcish_ task:
    when skill Orcish is at least 70
